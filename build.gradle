buildscript {
    ext.kotlin_version = '1.3.31'
    ext.coroutines_version = '1.2.1'
    ext.serialization_runtime_version = '0.10.0'
    ext.dokka_version = '0.9.18'
    ext.ktor_version = '1.1.5'
    ext.logback_version = '1.2.1'

    repositories {
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://jitpack.io' }
}

group 'org.baaahs'
version '0.0.1'

apply plugin: 'maven-publish'

kotlin {
    jvm()
    js()
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
//    linuxX64("linux")

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlin:kotlin-serialization:1.3.21"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_runtime_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "io.ktor:ktor-server-core:$ktor_version"
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-server-host-common:$ktor_version"
                implementation "io.ktor:ktor-websockets:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_runtime_version"
                implementation "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"
                implementation 'com.github.markaren:three.kt:v0.88-ALPHA-7'
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }

//        linuxMain {
//        }
//        linuxTest {
//        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-24463:
compileKotlinJvm {
    dependsOn ":copySheepModel"
}
task copySheepModel(type: Copy) {
    from('src/commonMain/resources')
    into 'build/classes/kotlin/jvm/main'
}

compileKotlinJs {
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
}

compileKotlinJvm {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlinJvm {
    kotlinOptions.jvmTarget = "1.8"
}

jsProcessResources { t ->
    t.dependsOn(":buildReactUI")

    t.from('build/webpack')
    t.from('build/classes/kotlin/js/main')
}

task serve {
    dependsOn(":jsJar")

    doLast {
        SimpleHttpFileServerFactory factory = new SimpleHttpFileServerFactory()
        HttpFileServer server = factory.start(new File("build/processedResources/js/main"), 8001)
        println("Server Started on http://localhost:${server.port}/index.html ctrc+c to kill it")
        java.lang.Thread.sleep(Long.MAX_VALUE);
    }
}

task installReactUImodules(type: Exec) {
    commandLine "npm", "install"
}

task buildReactUI(type: Exec) {
    inputs.file("package-lock.json").withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.file("webpack.config.js").withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir("src/jsMain/js").withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.dir("$buildDir/webpack")
    outputs.cacheIf { true }

    commandLine "$projectDir/node_modules/.bin/webpack-cli", "--config", "$projectDir/webpack.config.js", "--mode", "development", "--color"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    // default config apparently not compatible with kotlin-multiplatform :-(
    kotlinTasks { [] }
    sourceDirs = files('src/commonMain/kotlin') + files('src/jsMain/kotlin') + files('src/jvmMain/kotlin')
}

task runPinkyJvm(type: JavaExec) {
    dependsOn assemble
    main = 'baaahs.PinkyMainKt'

    classpath = files(kotlin.targets.jvm.compilations.main.output) + kotlin.targets.jvm.compilations.main.runtimeDependencyFiles
    args projectDir
}

task runBrainJvm(type: JavaExec) {
    dependsOn assemble
    main = 'baaahs.PinkyMainKt'

    classpath = files(kotlin.targets.jvm.compilations.main.output) + kotlin.targets.jvm.compilations.main.runtimeDependencyFiles
    args projectDir
}